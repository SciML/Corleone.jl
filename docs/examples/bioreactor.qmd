---
title: Bioreactor 
author: Carl Julius Martensen
date: last-modified
categories: 
  - ODE
description: This example introduces Corleone.
engines: ['julia']
julia:
  exeflags: ["--project=.."]
format: 
  html: default
  ipynb: default
---

```{julia}
using Corleone
using Corleone.ModelingToolkit
using Corleone.ModelingToolkit: t_nounits as t, D_nounits as D
using CairoMakie
```

```{julia}
@mtkmodel Bioreactor begin 
    @description "Bioreactor model"
    @variables begin
        X(t)=6.5, [description="Species X", tunable = false]
        S(t)=12.0, [description="Species S", tunable = false]
        P(t)=22.0, [description="Species P", tunable = false]
        F(t)=(40.0+28.7)/2, [description = "Feed", input = true, bounds = (28.7, 40.0)]
        μ(t)=0., [description="Rate", tunable=false]
    end 
    @constants begin 
        Di=0.15, [description="Dilution"]
        Kᵢ=22.0, [description="Rate coefficient"]
        Kₘ=1.2, [description="Rate coefficient"]
        Pₘ=50.0, [description="Rate coefficient"]
        Y=0.4, [description="Substrate to Biomass rate"]
        α=2.2, [description="Linear slope"]
        β=0.2, [description="Linear intercept"]
        μₘ=0.48, [description="Maximal growth rate"]
    end 
    @equations begin
        μ ~ μₘ*(1-P / Pₘ) * S / (Kₘ + S + S^2 / Kᵢ)
        D(X) ~ -Di*X+μ*X
        D(S) ~ Di*(F-S)-μ/Y * X
        D(P) ~ -Di*P + (α*μ+β)*X
    end 
    @costs begin 
        Symbolics.Integral(t in (0., 48.))(Di*(F-P)^2)
    end
    @consolidate begin
        (x...) -> first(x)[1]
    end
end;
```

```{julia}
reactor = Bioreactor(; name = :Bioreactor)
```

```{julia}
u = ModelingToolkit.getvar(reactor, :F, namespace = false)
control = DirectControlCallback(Num(u) => (; timepoints = collect(0.0:2.0:46.0)));
```

```{julia}
problem = OCProblemBuilder(reactor, control, ShootingGrid([0.,]), ForwardSolveInitialization());
expandedproblem = problem()
```

```{julia}
using Optimization, OptimizationMOI, Ipopt
using OrdinaryDiffEqTsit5, SciMLSensitivity
```

```{julia}
optimization_problem = OptimizationProblem{true}(expandedproblem, AutoForwardDiff(), Tsit5())
```

```{julia}
optimization_solution = solve(optimization_problem, Ipopt.Optimizer(),);
```

## Solution 

```{julia}
#| code-fold: true
#| renderings: [light, dark]

using CairoMakie

function result_plot_bioreactor(problem, u)
  sol = problem.f.f.predictor(u, saveat = 0.01);
  f = Figure(title = "LQR Results")
  ax = Axis(f[1,1], ylabel = "X(t)")
  plot!(sol[1], idxs = [:X])
  ax2 = Axis(f[1,2], ylabel = "S(t)")
  plot!(sol[1], idxs = [:S])
  ax3 = Axis(f[2,1], ylabel = "P(t)")
  plot!(sol[1], idxs = [:P])
  ax4 = Axis(f[2, 2], ylabel = "F(t)") 
  plot!(sol[1], idxs = [:F])
  linkxaxes!(ax, ax2, ax3, ax4)
  return f
end
# We want two outputs for the light and dark rendering. Hence we use display
update_theme!(theme_light());
f_light = result_plot_bioreactor(optimization_problem, optimization_solution.u)
display(f_light)
update_theme!(theme_dark());
f_dark = result_plot_bioreactor(optimization_problem, optimization_solution.u)
```

## Appendix

This document was rendered using Quarto CLI {{< version >}}.

```{julia}
#| code-fold: true
using Pkg

versioninfo()

Pkg.status() 
```
